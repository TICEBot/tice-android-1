ext {
    var = '3.5.1'

    configVar = { name ->
        if (project.hasProperty(name)) {
            logger.error("Using value from command line argument for {}", name)
            return project.property(name)
        }

        def envValue = System.getenv(name)
        if (envValue != null) {
            logger.error("Using environment variable as value for {}.", name)
            return envValue
        }

        Properties properties = new Properties()
        def file = project.rootProject.file('local.properties')
        if (file.exists()) {
            properties.load(file.newDataInputStream())
            def value = properties.getProperty(name)
            if (value != null) {
                logger.error("Using value from local configuration for {}.", name)
                return value
            } else {
                logger.error("Value for variable {} not defined via local configuration or environment variable. Falling back to empty string.", name)
                return ""
            }
        } else {
            logger.error("Local configuration file not found and environment variable not set. Falling back to empty string for variable {}.", name)
            return ""
        }
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        room_version = '2.4.0-beta01'
        nav_version = '2.3.5'
        kotlin_version = '1.5.31'
    }
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath 'com.google.gms:google-services:4.3.10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "org.jmailen.gradle:kotlinter-gradle:3.7.0"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.7.1.1"
        classpath "com.google.android.gms:oss-licenses-plugin:0.10.4"
        classpath 'gradle.plugin.com.cookpad.android.plugin:plugin:1.2.8'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = 'mapbox'
                password = configVar("MAPBOX_DOWNLOAD_TOKEN")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}